import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

base {
    archivesName = "${project.archives_name}-Fabric"
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation "org.yaml:snakeyaml:2.0"
    shadow 'org.yaml:snakeyaml:2.0'
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'org.yaml.snakeyaml', 'com.bunubbv.shadow.snakeyaml'
}

tasks.register("remapShadowJar", RemapJarTask) {
    dependsOn shadowJar
    group = "build"

    inputFile.set(shadowJar.archiveFile)
    archiveClassifier.set("")
}

processResources {
    filteringCharset "UTF-8"

    inputs.properties([
            version: project.version,
            minecraft_version: project.minecraft_version,
            loader_version: project.loader_version,
    ])

    filesMatching("fabric.mod.json") {
        expand(
                name: project.name,
                version: project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "author": project.author,
                "website": project.website,
        )
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_name
            from components.java
        }
    }
}
